def xs = range[1.0, grid_w, 1.0]
def ys = range[1.0, grid_h, 1.0]

def _superdot = {
    2.0, 28.0   ;
    27.0, 28.0  ;
    2.0, 8.0    ;
    27.0, 8.0   ;
}


// Top and Bottom rows are all walls
def _wall = x,1.0    from x in xs
def _wall = x,grid_h from x in xs

// Left and Right columns are all walls except tunnel
def _wall = 1.0,y    from y in ys where not 17.0 = y
def _wall = grid_w,y from y in ys where not 17.0 = y

def grid_w_i = float_int_convert[grid_w]
def grid_h_i = float_int_convert[grid_h]

// Just do the left half
def _wall_i_left = {
    // Tunnels
    ( {1;2;3;4;5;6}, {18;19;20;21;22} );
    ( {1;2;3;4;5;6}, {12;13;14;15;16} );

    //  TODO: Empty tunnel borders (need to disable dots)
    // ( {1;2;3;4;5;6}, {18} );
    // ( {1;2;3;4;5;6}, {22} );
    // ( {6}, {18;19;20;21;22} );

    // ( {1;2;3;4;5;6}, {16} );
    // ( {1;2;3;4;5;6}, {12} );
    // ( {6}, {12;13;14;15;16} );


    // Bumps
    (2,6); (2,7);
    (3,6); (3,7);

    // Inside squares
    (range[3,12,1] , {3;4});
    ({8;9} , range[5,7,1]);

    ({    5;6} , {6;7;8;9});
    ({3;4;5;6} , {    9;10});

    (range[8,12,1], {9;10});

    // Center T (stop halfway on this one)
    (range[11,14,1] , {6;7});
    ({14} , range[3,7,1]);

    // Center T (stop halfway on this one)
    (range[11,14,1] , {12;13});
    ({14} , range[9,13,1]);

    (range[8,9,1], range[12,16,1]);

    // ------ Top Half ----------
    // Center T (stop halfway on this one)
    (range[11,14,1] , {24;25});
    ({14} , range[21,25,1]);

    (range[8,9,1], range[18,25,1]);
    (range[10,12,1] , {21;22});

    (range[3,6,1], {24;25});

    (range[3,6,1], {27;28;29});

    (range[8,12,1], {27;28;29});

    // Vertical Bump
    ({14} , range[27,30,1]);

    // ------ Ghost Box ----------
    (range[11,14,1], {15});
    (range[11,14,1], {19});
    ({11}, range[15,grid_h_i-13,1]);
}

def _wall_i(x,y) = _wall_i_left(x,y) or _wall_i_left(grid_w_i-x+1, y)

def _wall = int_float_convert[x], int_float_convert[y] from (x,y) where _wall_i(x,y)

// clear any old environments if they already exist
def delete[:wall] = wall
def delete[:superdot] = superdot
def delete[:dot] = dot

// Insert new ones
def insert[:superdot] = _superdot
def insert[:wall] = _wall

def ghost_min_x = 9.0
def ghost_max_x = grid_w_i - 8.0
def ghost_min_y = 11.0
def ghost_max_y = grid_h_i - 8.0

def insert[:dot](x,y) =
    range[1.0,grid_w,1.0](x) and range[1.0,grid_h,1.0](y)
    and not _wall(x,y)
    and not _superdot(x, y)

    // Note that this is comparing against their initial positions, so the characters must
    // be initialized first.
    // Not any of the squares around pacman:
    and not exists(px,py :
            pacman_pos[:x](px) and pacman_pos[:y](py)
            and px > x - 1.0 and px < x + 1.0
            and py > y - 1.0 and py < y + 1.0
        )
    // Not any of the squares around ghosts
    and not(
        x > ghost_min_x and x < ghost_max_x and
        y > ghost_min_y and y < ghost_max_y
    )

